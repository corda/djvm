plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
    id 'net.corda.plugins.publish-utils'
    id 'com.jfrog.artifactory'
}

description 'Corda deterministic JVM sandbox command-line tool'

ext {
    djvmName = 'corda-djvm-cli'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-core:$log4j_version"
    implementation "com.jcabi:jcabi-manifests:$jcabi_manifests_version"

    // ClassGraph: classpath scanning
    implementation "io.github.classgraph:classgraph:$class_graph_version"

    implementation "info.picocli:picocli:$picocli_version"
    implementation project(path: ':djvm', configuration: 'shadow')
}

jar.enabled = false

shadowJar {
    baseName djvmName
    classifier ''
    manifest {
        attributes(
            'Automatic-Module-Name': 'net.corda.djvm.cli',
            'Main-Class': 'net.corda.djvm.tools.cli.Program',
            'Build-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Sealed': true
        )
    }
}

task shadowZip(type: Zip) {
    archiveBaseName = djvmName
    archiveClassifier = ''

    from(shadowJar) {
        rename "$djvmName-(.*).jar", "${djvmName}.jar"
    }
    from('src/shell/') {
        fileMode = 0755
    }
    zip64 true
}

assemble.dependsOn shadowZip

artifacts {
    publish shadowZip
}

publish {
    dependenciesFrom configurations.shadow
    publishSources = false
    publishJavadoc = false
    name shadowZip.baseName
}
